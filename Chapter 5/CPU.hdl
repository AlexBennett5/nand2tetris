/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

	Not(in=instruction[15], out=aInst);
	Mux16(a=ALUout, b=instruction, sel=aInst, out=Aregin);
	Or(a=aInst, b=instruction[5], out=loadA);
	ARegister(in=Aregin, load=loadA, out=Aregout, out[0..14]=addressM);
	
	And(a=instruction[15], b=instruction[12], out=AMsel);
	Mux16(a=Aregout, b=inM, sel=AMsel, out=AMout);

	And(a=instruction[15], b=instruction[4], out=loadD);
	DRegister(in=ALUout, load=loadD, out=Dregout);

	ALU(x=Dregout, y=AMout, zx=instruction[11], nx=instruction[10], 
	zy=instruction[9], ny=instruction[8], f=instruction[7], 
	no=instruction[6], out=ALUout, out=outM, zr=zero, ng=neg);
	And(a=instruction[15], b=instruction[3], out=writeM);
	
	Not(in=zero, out=notZero);
	Not(in=neg, out=notNeg);
	And(a=instruction[15], b=instruction[0], out=jgt);
	And(a=instruction[15], b=instruction[1], out=jeq);
	And(a=instruction[15], b=instruction[2], out=jlt);
	And(a=notZero, b=notNeg, out=pos);
	And(a=jgt, b=pos, out=j1);
	And(a=jeq, b=zero, out=j2);
	And(a=jlt, b=neg, out=j3);
	Or(a=j1, b=j2, out=j4);
	Or(a=j4, b=j3, out=loadPC);

	PC(in=Aregout, reset=reset, load=loadPC, inc=true, out[0..14]=pc);

}
